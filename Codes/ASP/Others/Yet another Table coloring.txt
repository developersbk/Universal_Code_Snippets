<%
    'The const's are For the colors and the Flag is needed
    Const sPrimaryColor = "WHITE"
    Const sSecondaryColor = "YELLOW"
    Dim bColorFlag
    Dim lCounter
    'This function does all the work For you.
    Function LineColor()
        bColorFlag = Not bColorFlag
        If bColorFlag Then
            LineColor = sPrimaryColor
        Else
            LineColor = sSecondaryColor
        End If
    End Function
%>
<SCRIPT language='VBScript'>
    Dim sRowOldColor
    Const sHighlightColor = "LIGHTBLUE"
    
    Sub Document_onclick()
        
        Dim sID
        
        'Need To show the parent of the element that was clicked on
        If Window.Event.srcElement.tagName = "TD" Then 
            sID = Window.Event.srcElement.ParentElement.id
            MsgBox "ID = " & sID,,"This is the ID of the row."
        End If      
    End Sub
    Sub Document_onmouseover()
        If Window.Event.srcElement.tagName = "TD" Then 
            sRowOldColor = window.event.srcElement.ParentElement.bgcolor
            window.event.SrcElement.ParentElement.bgcolor=sHighlightColor
        End If
    End Sub
    
    Sub Document_onmouseout ()
        If window.event.srcElement.tagName = "TD" Then 
            window.event.srcElement.parentElement.bgcolor = sRowOldColor
        End If
    End Sub
</SCRIPT>
<HTML>
    <HEAD>
        <TITLE>
            Alternating Line Colors 
        </TITLE>
    </HEAD>
    <BODY>
        <CENTER>
            <P>
                <BR>
            </P>
            <TABLE width='80%' cellspacing='3' cellpadding='3' border='1'>
            <%
                For lCounter = 1 To 10
                    Response.Write("<TR bgcolor='" & LineColor & "' id='" & lCounter & "'>" & vbCrLf)
                    Response.Write("<TD><CENTER>" & lCounter & "</CENTER></TD>" & vbCrLf)
                    Response.Write("</TR>" & vbCrLf)
                Next
            %>
            </TABLE>
        </CENTER>
    </BODY>
</HTML>
