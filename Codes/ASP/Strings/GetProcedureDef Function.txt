Function GetProcedureDef(ByVal connstring, ByVal procname)
    Dim cnn, cmd, prm, cat, d

    Set d = CreateObject("Scripting.Dictionary")
    Set cnn = CreateObject("ADODB.Connection")

    ' Open the Connection
    cnn.Open connstring

    ' Open the catalog
    Set cat = CreateObject("ADOX.Catalog")
    cat.ActiveConnection = cnn

    ' Get the command object
    Set cmd = cat.Procedures(procname).Command

    ' Retrieve Parameter information
    cmd.Parameters.Refresh
    For Each prm In cmd.Parameters
        d.Add prm.Name, prm.Type
    Next

    Set cmd = Nothing
    Set cat = Nothing
    Set cnn = Nothing

    Set GetProcedureDef = d
End Function
